plugins {
    id 'org.jetbrains.kotlin.jvm'
    id 'maven-publish'
}

def sdkGroupId = rootProject.sdkGroupId
def sdkArtifactId = 'sdk-kotlin'
def sdkVersion = '0.2-SNAPSHOT'
def sdkName = 'Cells Kotlin SDK'

java.sourceCompatibility = JavaVersion.VERSION_17
java.targetCompatibility = JavaVersion.VERSION_17

// Also publish sources and Javadoc
java {
    withJavadocJar()
    withSourcesJar()
}

sourceSets {
    test {
        java.srcDirs = ['src/test/kotlin']
    }
}

// Skip integration tests by default
// test {
//     if (System.properties['test.profile'] != 'integration') {
//         exclude '**/integration/*'
//     }
// }

compileKotlin {
    kotlinOptions {
        jvmTarget = "17"
    }
}
compileTestKotlin {
    kotlinOptions {
        jvmTarget = "17"
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            groupId = sdkGroupId
            artifactId = sdkArtifactId
            version = sdkVersion

            pom {
                name = sdkName
                description = "A collection of Kotlin libraries for seamless communication with the Pydio Cells server stack"
                url = rootProject.mainUrl
                rootProject.configurePom(pom)
            }
        }
    }
}

jar {
    manifest {
        attributes(
                'Specification-Title': sdkName,
                'Specification-Vendor': rootProject.sdkVendor,
                'Implementation-Title': project.name,
                'Implementation-Vendor': rootProject.sdkVendor,
                'Implementation-Version': sdkVersion,
        )
    }
}

dependencies {

    implementation project(':sdk-openapi')

    // Set of carefully chosen third-party libraries, do not update lightly.
    implementation libs.gson
    implementation libs.okhttp
    implementation libs.ktor.core
    implementation libs.ktor.okhttp
    implementation libs.kotlinx.coroutines.core

    implementation libs.urlencoder

    implementation("org.apache.logging.log4j:log4j-slf4j2-impl:2.20.0")

    // Java SDK
    implementation("software.amazon.awssdk:aws-sdk-java:2.28.21")
    // Kotlin SDK => AWS as SAAS specific
    implementation("aws.sdk.kotlin:s3:1.0.41")
    implementation("aws.smithy.kotlin:http-client-engine-okhttp:1.3.16")
    // Android SDK => android specific. Not good.
    //    implementation libs.aws.sdk.s3
    //    implementation("androidx.appcompat:appcompat:1.2.0")

    // Minio Java
    implementation("io.minio:minio:8.5.12")

    testImplementation libs.junit
    testImplementation libs.kotlinx.coroutines.test
    // To load target server configurations
    // testImplementation 'org.yaml:snakeyaml:2.3'

}

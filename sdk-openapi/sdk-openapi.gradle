plugins {
    id 'java-library'
    id 'org.jetbrains.kotlin.jvm'
    id 'maven-publish'
}

def sdkSwaggerSpecVersion = '4.4.6'
def sdkVendor = 'Abstrium SAS'

def sdkGroupId = 'com.pydio.kotlin'
def sdkArtifactId = 'sdk-openapi'
def sdkVersion = '0.1-SNAPSHOT'
def sdkName = 'Cells OpenAPI SDK for Kotlin'

java.sourceCompatibility = JavaVersion.VERSION_17
java.targetCompatibility = JavaVersion.VERSION_17

compileKotlin {
    kotlinOptions {
        jvmTarget = "17"
    }
}
compileTestKotlin {
    kotlinOptions {
        jvmTarget = "17"
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            groupId = sdkGroupId
            artifactId = sdkArtifactId
            version = sdkVersion
            pom {
                name = sdkName
                description = "Cells Kotlin SDK generated by the OpenAPI generator tool directly from the Swagger specifications v$sdkSwaggerSpecVersion"
                url = rootProject.mainUrl
                rootProject.configurePom(pom)
            }
        }
    }
}

jar {
    manifest {
        attributes('Specification-Title': sdkName,
                'Specification-Version': sdkSwaggerSpecVersion,
                'Specification-Vendor': sdkVendor,
                'Implementation-Title': project.name,
                'Implementation-Vendor': sdkVendor,
                'Implementation-Version': sdkVersion)
    }
}

dependencies {
    implementation libs.kotlin.stdlib.jdk8
    implementation libs.kotlin.reflect
    implementation libs.moshi.kotlin
    implementation libs.moshi.adapters
    implementation libs.okhttp
    implementation libs.logging.interceptor
}
